apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 29

    defaultConfig {
        // versionCode and versionName must be hardcoded to support F-droid
        versionCode 701001
        versionName '7.1.0'
        applicationId 'dev.lucanlm.antimine'
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        resConfigs 'en', 'cs', 'de', 'el', 'es', 'fr', 'ja', 'pt', 'ru', 'tr', 'uk', 'vi', 'zh'
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            animationsDisabled true
        }
    }

    kapt {
        correctErrorTypes true
    }

    flavorDimensions 'version'
    productFlavors {
        google {
            dimension 'version'
        }

        foss {
            dimension 'version'
        }
    }
}

dependencies {
    // Dependencies must be hardcoded to support F-droid

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')

    googleImplementation project(':proprietary')
    fossImplementation project(':foss')

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Constraint
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta7'

    // Google
    implementation 'com.google.android.material:material:1.1.0'

    // Dagger
    implementation 'com.google.dagger:hilt-android:2.28-alpha'
    kapt 'com.google.dagger:hilt-android-compiler:2.28-alpha'
    testImplementation 'com.google.dagger:hilt-android-testing:2.28-alpha'
    kaptTest 'com.google.dagger:hilt-android-compiler:2.28-alpha'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.72'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.5'

    // Tests
    testImplementation 'junit:junit:4.13'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test:core-ktx:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    testImplementation 'androidx.fragment:fragment-testing:1.2.5'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.mockito:mockito-core:2.24.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}
