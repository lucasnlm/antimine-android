plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

if (System.getenv('IS_GOOGLE_BUILD')) {
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}

android {
    compileSdkVersion 33

    defaultConfig {
        // versionCode and versionName must be hardcoded to support F-droid
        versionCode 1000001
        versionName '16.0.0'
        minSdkVersion 21
        targetSdkVersion 33
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    bundle {
        language {
            // Disable language split to support extra languages that google removes
            // when building aab.
            enableSplit = false
        }
    }

    signingConfigs {
        release {
            if (System.getenv('IS_RELEASE_BUILD')) {
                storeFile file(System.getenv('KEYSTORE'))
                keyAlias System.getenv('KEY_ALIAS')
                storePassword System.getenv('KEY_STORE_PASSWORD')
                keyPassword System.getenv('KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-Xuse-experimental=kotlinx.coroutines.FlowPreview']
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            animationsDisabled true
        }
    }

    flavorDimensions 'version'
    productFlavors {
        google {
            dimension 'version'
            applicationId 'dev.lucasnlm.antimine'
        }

        googleInstant {
            dimension 'version'
            applicationId 'dev.lucasnlm.antimine'
            versionNameSuffix "-inst"
        }

        foss {
            dimension 'version'
            // There's a typo on F-Droid release :(
            applicationId 'dev.lucanlm.antimine'
        }
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    // Dependencies must be hardcoded to support F-droid

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':external')
    implementation project(':common')
    implementation project(':control')
    implementation project(':about')
    implementation project(':ui')
    implementation project(':preferences')
    implementation project(':themes')
    implementation project(':tutorial')
    implementation project(':core')
    implementation project(':gdx')

    googleImplementation project(':proprietary')
    googleInstantImplementation project(':proprietary')
    googleInstantImplementation project(':instant')
    fossImplementation project(':foss')

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.activity:activity-ktx:1.5.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.2'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.5.1'

    // Room
    implementation 'androidx.room:room-ktx:2.4.3'

    // Constraint
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Google
    implementation 'com.google.android.material:material:1.6.1'

    // Koin
    implementation 'io.insert-koin:koin-android:3.1.2'
    testImplementation 'io.insert-koin:koin-test:3.1.2'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.10'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation 'androidx.core:core-ktx:1.9.0'
    testImplementation 'androidx.test:core-ktx:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.fragment:fragment-testing:1.5.2'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'io.mockk:mockk:1.11.0'

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
    }
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}
